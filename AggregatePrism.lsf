#!/bin/csh
#
# This script reads the full-precision PRISM data and aggregates it
# to the same locations as the gcm data. The gcm data is global,
# the PRISM data is Continental US only ... 
#
# $Id: AggregatePrism.lsf,v 1.12 2008/04/24 03:57:34 thoar Exp $
#----------------------------------------------------------------------
# LSF options for BSUB
#
### -J      job name 
### -o      output listing filename 
### -q      queue
### -N -u   mail this user when job finishes
### -n      number of processors  (really)
### -W      wall-clock hours:minutes required
#
#----------------------------------------------------------------------
#
#BSUB -J aggregate[1-12]
#BSUB -o aggregate.%J.%I.log
#BSUB -q standby
#BSUB -N -u ${USER}@ucar.edu
#BSUB -n 1
#BSUB -W 0:10
#
#----------------------------------------------------------------------
# End of script preamble.
#----------------------------------------------------------------------

#----------------------------------------------------------------------
# Turns out the scripts are a lot more flexible if you don't rely on 
# the queuing-system-specific variables -- so I am converting them to
# 'generic' names and using the generics throughout the remainder.
#----------------------------------------------------------------------

if ($?LSB_HOSTS) then

   setenv ORIGINALDIR $LS_SUBCWD 
   setenv JOBNAME     $LSB_OUTPUTFILE:ar
   setenv JOBID       $LSB_JOBID
   setenv MYQUEUE     $LSB_QUEUE
   setenv MYHOST      $LSB_SUB_HOST
   setenv TASKID      $LSB_JOBINDEX
   setenv NTASKS      $LSB_JOBINDEX_END
   setenv TEMPDIR     /ptmp/thoar

   # if you are interested in the environment variables set
   # by LSF, uncomment this following line. 
   # env | grep LS | grep -v LS_COLORS | sort

else

   #-------------------------------------------------------------------
   # You can debug the script by running this interactively.
   # All the environment variables normally specified by the queueing 
   # system must be manually set ... here.
   # PLEASE only run VERY SHORT jobs this way.
   #-------------------------------------------------------------------

   setenv ORIGINALDIR `pwd`
   setenv JOBNAME     aggregate
   setenv JOBID       $$
   setenv MYQUEUE     Interactive
   setenv MYHOST      $host
   setenv TASKID      1
   setenv NTASKS      12
   setenv TEMPDIR     /project/gsp/thoar
   setenv TEMPDIR     /ptmp/thoar

endif

#----------------------------------------------------------------------
# good style is to create a clean temporary directory, work in it, and
# clean it up when you are done.
#----------------------------------------------------------------------

setenv TMPDIR ${TEMPDIR}/${JOBNAME}_job${TASKID}

mkdir -p ${TMPDIR}
cd ${TMPDIR}
\rm -f *.nc

set experiment = ${JOBNAME}.${JOBID}.${TASKID}

#----------------------------------------------------------------------
# Just an echo of job attributes
# I am also leaving a trail of breadcrumbs in case the job fails.
# I'll be able to track which node was being used when it failed.
#----------------------------------------------------------------------

echo
echo "${JOBNAME} ($JOBID) submitted   from $ORIGINALDIR"
echo "${JOBNAME} ($JOBID) submitted   from $MYHOST"
echo "${JOBNAME} ($JOBID) running in queue $MYQUEUE"
echo "${JOBNAME} ($JOBID) running       on $host"
echo "${JOBNAME} ($JOBID) job $TASKID of $NTASKS started at "`date`
echo

echo "${JOBNAME} ($JOBID) submitted   from $ORIGINALDIR"            >! breadcrumb
echo "${JOBNAME} ($JOBID) submitted   from $MYHOST"                 >> breadcrumb
echo "${JOBNAME} ($JOBID) running in queue $MYQUEUE"                >> breadcrumb
echo "${JOBNAME} ($JOBID) running       on $host"                   >> breadcrumb
echo "${JOBNAME} ($JOBID) job $TASKID of $NTASKS started at "`date` >> breadcrumb

#----------------------------------------------------------------------
# Set up the arrays that will be exploited by the Job Array ID 
#----------------------------------------------------------------------

set  startyears = ( 1895 1900 1910 1920 1930 1940 1950 1960 1970 1980 1990 2000 )
set  endyears   = ( 1899 1909 1919 1929 1939 1949 1959 1969 1979 1989 1999 2005 )

#----------------------------------------------------------------------
# Create a teeny-weeny R program to run
#----------------------------------------------------------------------

echo ' library(ncdf)'                                                    >! rscript.r
echo ' source("/fs/image/home/thoar/downscaling/ConvertUnits.r")'        >> rscript.r
echo ' source("/fs/image/home/thoar/downscaling/GetArcInfoMetadata.r")'  >> rscript.r
echo ' source("/fs/image/home/thoar/downscaling/Prism2gcmRData.r")'      >> rscript.r
echo ' source("/fs/image/home/thoar/downscaling/AggregateRDataToNetCDF.r")' >> rscript.r
echo ""                                                                  >> rscript.r
echo ' prismdir = "'${TEMPDIR}/downscaling/prism100'"'                   >> rscript.r
echo ' ddir     = "'/fs/image/home/thoar/downscaling'"'                  >> rscript.r
echo ' locator  = FALSE'                                                 >> rscript.r
echo ' unlim    = TRUE'                                                  >> rscript.r
echo " year1    = $startyears[$TASKID]"                                  >> rscript.r
echo " yearN    = $endyears[$TASKID]"                                    >> rscript.r

#foreach VAR ( tmin tmax tave dtr ppt )
foreach VAR ( tave )

  echo ' prismvar = "'$VAR'"'                                              >> rscript.r
  echo ' bob = Prism2gcmRData(ddir,prismdir,locator,prismvar,year1,yearN)' >> rscript.r
  echo ' ted = sprintf("%s/prismaggregated2_%s_%d_%d.RData",'               >> rscript.r
  echo '                     prismdir,prismvar,year1,yearN)'               >> rscript.r
  echo ' fname = sprintf("prismaggregated2_%s_%d_%d.nc",'                   >> rscript.r
  echo '                           prismvar,year1,yearN)'                  >> rscript.r
  echo ' save.image(file=ted)'                                             >> rscript.r
  echo ' ted = AggregateRDataToNetCDF(vrbl=bob, ddir=prismdir,'            >> rscript.r
  echo '                           file=fname, unlim=unlim)'               >> rscript.r

  R CMD BATCH --no-save rscript.r batch.rout

  mv rscript.r    ${ORIGINALDIR}/${experiment}.${VAR}.r
  mv batch.rout   ${ORIGINALDIR}/${experiment}.${VAR}.rout

end

#----------------------------------------------------------------------
cd ${ORIGINALDIR}
\rm -rf ${TMPDIR}

echo ""
echo "${JOBNAME} ($JOBID) job $TASKID of $NTASKS finished at "`date`
echo ""
