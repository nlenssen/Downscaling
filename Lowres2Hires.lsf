#!/bin/csh
#
# $Id: Lowres2Hires.lsf,v 1.8 2008/04/24 03:57:35 thoar Exp $
# 
#----------------------------------------------------------------------
# LSF options for BSUB
#
### -J      job name 
### -o      output listing filename 
### -q      queue
### -N -u   mail this user when job finishes
### -n      number of processors  (really)
### -W      wall-clock hours:minutes required
#
#----------------------------------------------------------------------
#
#XXXX -b 10:5:17:00
#XXXX -J low2hi[1-12]
#BSUB -J low2hi[1-12]
#BSUB -o low2hi.%J.%I.log
#BSUB -q standby
#BSUB -N -u ${USER}@ucar.edu
#BSUB -n 1
#BSUB -W 4:00
#
#----------------------------------------------------------------------
# End of script preamble.
#----------------------------------------------------------------------

#----------------------------------------------------------------------
# Turns out the scripts are a lot more flexible if you don't rely on 
# the queuing-system-specific variables -- so I am converting them to
# 'generic' names and using the generics throughout the remainder.
#----------------------------------------------------------------------

if ($?LSB_HOSTS) then

   setenv ORIGINALDIR $LS_SUBCWD 
   setenv JOBNAME     $LSB_OUTPUTFILE:ar
   setenv JOBID       $LSB_JOBID
   setenv MYQUEUE     $LSB_QUEUE
   setenv MYHOST      $LSB_SUB_HOST
   setenv TASKID      $LSB_JOBINDEX
   setenv NTASKS      $LSB_JOBINDEX_END
   setenv TEMPDIR     /ptmp/thoar

   # if you are interested in the environment variables set
   # by LSF, uncomment this following line. 
   # env | grep LS | grep -v LS_COLORS | sort

else

   #-------------------------------------------------------------------
   # You can debug the script by running this interactively.
   # All the environment variables normally specified by the queueing 
   # system must be manually set ... here.
   # PLEASE only run VERY SHORT jobs this way.
   #-------------------------------------------------------------------

   setenv ORIGINALDIR `pwd`
   setenv JOBNAME     low2hi
   setenv JOBID       $$
   setenv MYQUEUE     Interactive
   setenv MYHOST      $host
   setenv TASKID      2
   setenv NTASKS      3
   setenv TEMPDIR     /project/gsp/thoar
   setenv TEMPDIR     /ptmp/thoar

endif

#----------------------------------------------------------------------
# Set up the arrays that will be exploited by the Job Array ID 
#----------------------------------------------------------------------

set  startyears = ( 1895 1900 1910 1920 1930 1940 1950 1960 1970 1980 1990 2000 )
set  endyears   = ( 1899 1909 1919 1929 1939 1949 1959 1969 1979 1989 1999 2005 )
set year1 = $startyears[$TASKID]
set yearN = $endyears[$TASKID]

set experiment = ${JOBNAME}.${JOBID}.${TASKID}

#----------------------------------------------------------------------
# good style is to create a clean temporary directory, work in it, and
# clean it up when you are done.
#----------------------------------------------------------------------

setenv TMPDIR ${TEMPDIR}/${JOBNAME}_job${TASKID}

mkdir -p ${TMPDIR}
cd ${TMPDIR}
\rm -f *.nc

#----------------------------------------------------------------------
# Just an echo of job attributes
# I am also leaving a trail of breadcrumbs in case the job fails.
# I'll be able to track which node was being used when it failed.
#----------------------------------------------------------------------

echo
echo "${JOBNAME} ($JOBID) submitted   from $ORIGINALDIR"
echo "${JOBNAME} ($JOBID) submitted   from $MYHOST"
echo "${JOBNAME} ($JOBID) running in queue $MYQUEUE"
echo "${JOBNAME} ($JOBID) running       on $host"
echo "${JOBNAME} ($JOBID) job $TASKID of $NTASKS started at "`date`
echo

echo "${JOBNAME} ($JOBID) submitted   from $ORIGINALDIR"            >! breadcrumb
echo "${JOBNAME} ($JOBID) submitted   from $MYHOST"                 >> breadcrumb
echo "${JOBNAME} ($JOBID) running in queue $MYQUEUE"                >> breadcrumb
echo "${JOBNAME} ($JOBID) running       on $host"                   >> breadcrumb
echo "${JOBNAME} ($JOBID) job $TASKID of $NTASKS started at "`date` >> breadcrumb

#----------------------------------------------------------------------
# Create a teeny-weeny R program to run
#----------------------------------------------------------------------

echo ' library(ncdf)'                                                    >! rscript.r
echo ' library(fields)'                                                  >> rscript.r
echo ' source("/fs/image/home/thoar/downscaling/GetPredDomain.r")'       >> rscript.r
echo ' source("/fs/image/home/thoar/downscaling/ConvertUnits.r")'       >> rscript.r
echo ' source("/fs/image/home/thoar/downscaling/SplinePredict.r")'       >> rscript.r
echo ' source("/fs/image/home/thoar/downscaling/SimpleRDataToNetCDF.r")' >> rscript.r
echo ""                                                                  >> rscript.r
echo ' ddir    = "'${TEMPDIR}/downscaling/aggregated'"'                  >> rscript.r
echo " year1   = $startyears[$TASKID]"                                   >> rscript.r
echo " yearN   = $endyears[$TASKID]"                                     >> rscript.r

#foreach VAR ( tmin tmax tave dtr ppt )
foreach VAR ( tave )

   echo ' gcmvar  = "'$VAR'"'                                            >> rscript.r
   echo ' gcmfile = "'prismaggregated2_${VAR}_${year1}_${yearN}.nc'"'     >> rscript.r
   echo ' psmfile =    "'prismsplined2_${VAR}_${year1}_${yearN}.nc'"'     >> rscript.r
   echo ''                                                               >> rscript.r
   echo ' ted = sprintf("%s/splined_%s_%d_%d.RData",'                    >> rscript.r
   echo '               ddir, gcmvar, year1, yearN)'                     >> rscript.r
   echo ''                                                               >> rscript.r
   echo ' bob = SplinePredict(      ddir = ddir,'                        >> rscript.r
   echo '                     coarsefile = gcmfile,'                     >> rscript.r
   echo '                      coarsevar = gcmvar,'                      >> rscript.r
   echo '                          year1 = year1,'                       >> rscript.r
   echo '                          yearN = yearN)'                       >> rscript.r
   echo ''                                                               >> rscript.r
   echo ' bob$varprecision = "single"'                                   >> rscript.r
   echo ''                                                               >> rscript.r
   echo ' save.image(file=ted)'                                          >> rscript.r 
   echo ''                                                               >> rscript.r
   echo ' SimpleRDataToNetCDF(vrbl=bob, ddir=ddir, file=psmfile)'        >> rscript.r

   R CMD BATCH --no-save rscript.r batch.rout

   mv rscript.r    ${ORIGINALDIR}/${experiment}.${VAR}.r
   mv batch.rout   ${ORIGINALDIR}/${experiment}.${VAR}.rout

end

#----------------------------------------------------------------------
cd ${ORIGINALDIR}
\rm -rf ${TMPDIR}

echo ""
echo "${JOBNAME} ($JOBID) job $TASKID of $NTASKS finished at "`date`
echo ""
